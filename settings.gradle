pluginManagement {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
    }
    versionCatalogs {
        libs {
            version('minSdk', '24')
            version('targetSdk', '33')
            version('compileSdk', '33')

            version('jvm', '1.8')
            version('composeCompiler', '1.4.2')

            version('agp', '7.4.1')
            version('kotlin', '1.8.10')
            version('compose', "1.3.3")
            version('material3', '1.1.0-alpha06')
            version('androidX-core', '1.9.0')
            version('androidX-activity', '1.6.1')
            version('lifecycle', '2.5.1')
            version('room', '2.5.0')
            version('coroutines', '1.6.4')
            version('ksp', '1.8.10-1.0.9')
            version('retrofit', '2.9.0')
            version('okhttp', '4.10.0')
            version('hilt', '2.44.2')
            version('dataStore', '1.0.0')
            version('navigation', '2.5.3')
            

            plugin('android-application', 'com.android.application').versionRef('agp')
            plugin('android-library', 'com.android.library').versionRef('agp')
            plugin('kotlin-android', 'org.jetbrains.kotlin.android').versionRef('kotlin')
            plugin('ksp', 'com.google.devtools.ksp').versionRef('ksp')
            plugin('kapt', 'org.jetbrains.kotlin.kapt').versionRef('kotlin')
            plugin('hilt', 'com.google.dagger.hilt.android').versionRef('hilt')

            library('androidX-core', 'androidx.core', 'core-ktx').versionRef('androidX-core')
            library('androidX-activity-compose', 'androidx.activity', 'activity-compose').versionRef('androidX-activity')
            library('lifecycle-runtime', 'androidx.lifecycle', 'lifecycle-runtime-ktx').versionRef('lifecycle')

            library('compose-ui', 'androidx.compose.ui', 'ui').versionRef('compose')
            library('compose-ui-preview', 'androidx.compose.ui', 'ui-tooling-preview').versionRef('compose')
            library('material3', 'androidx.compose.material3', 'material3').versionRef('material3')

            library('datastore', 'androidx.datastore', 'datastore-preferences').versionRef('dataStore')

            bundle('compose', ['compose-ui', 'compose-ui-preview'])
            bundle('ui', ['compose-ui', 'compose-ui-preview', 'material3'])

            library('room-runtime', 'androidx.room', 'room-runtime').versionRef('room')
            library('room-ktx', 'androidx.room', 'room-ktx').versionRef('room')
            bundle('room', ['room-runtime', 'room-ktx'])
            library('room-compiler', 'androidx.room', 'room-compiler').versionRef('room')
            library('okhttp-logger', 'com.squareup.okhttp3', 'logging-interceptor').versionRef('okhttp')

            library('coroutines-core', 'org.jetbrains.kotlinx', 'kotlinx-coroutines-core').versionRef('coroutines')
            library('coroutines-android', 'org.jetbrains.kotlinx', 'kotlinx-coroutines-android').versionRef('coroutines')
            bundle('coroutines', ['coroutines-core', 'coroutines-android'])

            library('retrofit', 'com.squareup.retrofit2', 'retrofit').versionRef('retrofit')
            library('retrofit-moshi', 'com.squareup.retrofit2', 'converter-moshi').versionRef('retrofit')
            library('moshi-codegen', 'com.squareup.moshi', 'moshi-kotlin-codegen').version('1.8.0')
            bundle('retrofit', ['retrofit', 'retrofit-moshi'])

            library('hilt-android', 'com.google.dagger', 'hilt-android').versionRef('hilt')
            library('hilt-compiler', 'com.google.dagger', 'hilt-android-compiler').versionRef('hilt')

            library("navigation-compose", "androidx.navigation", "navigation-compose").versionRef('navigation')
            library("hilt-navigation", "androidx.hilt", "hilt-navigation-compose").version('1.0.0')
            library("navigation-material", "com.google.accompanist", "accompanist-navigation-material").version('0.24.10-beta')
            bundle('navigation', ['navigation-compose', 'hilt-navigation', 'navigation-material'])

            library('core-testing','androidx.arch.core','core-testing').version('2.2.0')
        }
    }
}
rootProject.name = "Getcamp"

include ':app'
include ':feature:people', ':feature:schedule', ':feature:setting'
include ':library:database', ':library:network', ':library:utility', ':library:navigator', ':library:cache'
